dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

dnl ================================================================
dnl Package version (i.e. the pretty number that users see)
dnl ================================================================
m4_define([gx_major_version], [0])
m4_define([gx_minor_version], [1])
m4_define([gx_micro_version], [0])
m4_define([gx_version],
          [gx_major_version.gx_minor_version.gx_micro_version])

dnl ================================================================
dnl Interface version details for libtool
dnl ================================================================
# NB: I didn't copy the widely used glib/gtk style of dealing with libtool
# versioning since I found the different (compared to libtool's documentation)
# ideas for what the interface age should represent; and the addition of a
# binary age confusing. I couldn't find any documentation/mailing list archives
# etc explaing the ideas nor any code that makes use of those values. They also
# seem to go against the advice of not basing the libtool versioning on package
# versioning.
#
# For reference on how the various numbers should be updated at release time
# these rules are adapted from the libtool info pages:
#
#  1. Update the version information only immediately before a public
#     release of your software.
#
#  2. If the library source code has changed at all since the last
#     update, then increment REVISION (`C:R:A' becomes `C:r+1:A').
#
#  3. If any interfaces have been added, removed, or changed since the
#     last update, increment CURRENT, and set REVISION to 0.
#
#  4. If any interfaces have been added since the last public release,
#     then increment AGE.
#
#  5. If any interfaces have been removed since the last public release,
#     then set AGE to 0.
m4_define([gx_lt_current], 0)
m4_define([gx_lt_revision], 0)
m4_define([gx_lt_age], 0)
# We do also tell libtool the pretty version also:
m4_define([gx_lt_release], [gx_version])

AC_INIT(gx, [gx_version])
AC_CONFIG_SRCDIR(src/gx-connection.c)
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE([gnu])

dnl ================================================================
dnl More version number stuff
dnl ================================================================
GX_MAJOR_VERSION=gx_major_version
GX_MINOR_VERSION=gx_minor_version
GX_MICRO_VERSION=gx_micro_version
GX_VERSION=gx_version

AC_SUBST(GX_MAJOR_VERSION)
AC_SUBST(GX_MINOR_VERSION)
AC_SUBST(GX_MICRO_VERSION)
AC_SUBST(GX_VERSION)


dnl ================================================================
dnl libtool versioning
dnl ================================================================
GX_LT_CURRENT=gx_lt_current
GX_LT_REVISION=gx_lt_revision
GX_LT_AGE=gx_lt_age
GX_LT_RELEASE=gx_lt_release
AC_SUBST(GX_LT_CURRENT)
AC_SUBST(GX_LT_REVISION)
AC_SUBST(GX_LT_AGE)
AC_SUBST(GX_LT_RELEASE)

AM_MAINTAINER_MODE


dnl ================================================================
dnl Handle extra configure options
dnl ================================================================

AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug], [enable debuging messages]),
  [
   if test $enableval = yes; then
     AC_DEFINE(GXGEN_DEBUG, 1, "define to enable debuging")
     CFLAGS="-g3 -O0"
   else
    if test -z $CFLAGS; then
     CFLAGS="-g3 -O2"
    fi
   fi
  ],
  [
   if test -z $CFLAGS; then
    CFLAGS="-g3 -O2"
   fi
  ])


AC_ARG_ENABLE(werror,
  AC_HELP_STRING([--enable-werror], [Use -Werror option for compilation]),
  [
      if test $enableval = yes; then
         EXTRA_CFLAGS="-Wall -Werror"
      else
         EXTRA_CFLAGS="-Wall"
      fi
  ],
  [
   if test -z $EXTRA_CFLAGS; then
      EXTRA_CFLAGS="-Wall"
   fi
  ])

AC_ARG_ENABLE(uninstalled-build,
              AC_HELP_STRING([--enable-uninstalled-build],
			     [Search for files in build directory as well]),
	      enable_uninstalled=yes,)
if test x"$enable_uninstalled" = xyes; then
	AC_DEFINE_UNQUOTED(
        SHARE_UNINSTALLED_DIR,"`pwd`/src/data",[path to source data dir])
fi

AC_ARG_ENABLE(composite, AS_HELP_STRING([--enable-composite], [Build XCB Composite Extension (default: yes)]), [BUILD_COMPOSITE=$enableval], [BUILD_COMPOSITE=yes])
AM_CONDITIONAL(BUILD_COMPOSITE, [test "x$BUILD_COMPOSITE" = xyes])
AC_ARG_ENABLE(damage, AS_HELP_STRING([--enable-damage], [Build XCB Damage Extension (default: yes)]), [BUILD_DAMAGE=$enableval], [BUILD_DAMAGE=yes])
AM_CONDITIONAL(BUILD_DAMAGE, [test "x$BUILD_DAMAGE" = xyes])
AC_ARG_ENABLE(dpms, AS_HELP_STRING([--enable-dpms], [Build XCB DPMS Extension (default: yes)]), [BUILD_DPMS=$enableval], [BUILD_DPMS=yes])
AM_CONDITIONAL(BUILD_DPMS, [test "x$BUILD_DPMS" = xyes])
AC_ARG_ENABLE(glx, AS_HELP_STRING([--enable-glx], [Build XCB GLX Extension (default: yes)]), [BUILD_GLX=$enableval], [BUILD_GLX=yes])
AM_CONDITIONAL(BUILD_GLX, [test "x$BUILD_GLX" = xyes])
AC_ARG_ENABLE(randr, AS_HELP_STRING([--enable-randr], [Build XCB RandR Extension (default: yes)]), [BUILD_RANDR=$enableval], [BUILD_RANDR=yes])
AM_CONDITIONAL(BUILD_RANDR, [test "x$BUILD_RANDR" = xyes])
AC_ARG_ENABLE(record, AS_HELP_STRING([--enable-record], [Build XCB Record Extension (default: yes)]), [BUILD_RECORD=$enableval], [BUILD_RECORD=yes])
AM_CONDITIONAL(BUILD_RECORD, [test "x$BUILD_RECORD" = xyes])
AC_ARG_ENABLE(render, AS_HELP_STRING([--enable-render], [Build XCB Render Extension (default: yes)]), [BUILD_RENDER=$enableval], [BUILD_RENDER=yes])
AM_CONDITIONAL(BUILD_RENDER, [test "x$BUILD_RENDER" = xyes])
AC_ARG_ENABLE(res, AS_HELP_STRING([--enable-res], [Build XCB X-Resource Extension (default: yes)]), [BUILD_RES=$enableval], [BUILD_RES=yes])
AM_CONDITIONAL(BUILD_RES, [test "x$BUILD_RES" = xyes])
AC_ARG_ENABLE(screensaver, AS_HELP_STRING([--enable-screensaver], [Build XCB Screensaver Extension (default: yes)]), [BUILD_SCREENSAVER=$enableval], [BUILD_SCREENSAVER=yes])
AM_CONDITIONAL(BUILD_SCREENSAVER, [test "x$BUILD_SCREENSAVER" = xyes])
AC_ARG_ENABLE(shape, AS_HELP_STRING([--enable-shape], [Build XCB Shape Extension (default: yes)]), [BUILD_SHAPE=$enableval], [BUILD_SHAPE=yes])
AM_CONDITIONAL(BUILD_SHAPE, [test "x$BUILD_SHAPE" = xyes])
AC_ARG_ENABLE(shm, AS_HELP_STRING([--enable-shm], [Build XCB Shm Extension (default: yes)]), [BUILD_SHM=$enableval], [BUILD_SHM=yes])
AM_CONDITIONAL(BUILD_SHM, [test "x$BUILD_SHM" = xyes])
AC_ARG_ENABLE(sync, AS_HELP_STRING([--enable-sync], [Build XCB Sync Extension (default: yes)]), [BUILD_SYNC=$enableval], [BUILD_SYNC=yes])
AM_CONDITIONAL(BUILD_SYNC, [test "x$BUILD_SYNC" = xyes])
AC_ARG_ENABLE(xevie, AS_HELP_STRING([--enable-xevie], [Build XCB Xevie Extension (default: yes)]), [BUILD_XEVIE=$enableval], [BUILD_XEVIE=yes])
AM_CONDITIONAL(BUILD_XEVIE, [test "x$BUILD_XEVIE" = xyes])
AC_ARG_ENABLE(xf86dri, AS_HELP_STRING([--enable-xf86dri], [Build XCB XFree86-DRI Extension (default: yes)]), [BUILD_XF86DRI=$enableval], [BUILD_XF86DRI=yes])
AM_CONDITIONAL(BUILD_XF86DRI, [test "x$BUILD_XF86DRI" = xyes])
AC_ARG_ENABLE(xfixes, AS_HELP_STRING([--enable-xfixes], [Build XCB XFixes Extension (default: yes)]), [BUILD_XFIXES=$enableval], [BUILD_XFIXES=yes])
AM_CONDITIONAL(BUILD_XFIXES, [test "x$BUILD_XFIXES" = xyes])
AC_ARG_ENABLE(xinerama, AS_HELP_STRING([--enable-xinerama], [Build XCB Xinerama Extension (default: yes)]), [BUILD_XINERAMA=$enableval], [BUILD_XINERAMA=yes])
AM_CONDITIONAL(BUILD_XINERAMA, [test "x$BUILD_XINERAMA" = xyes])
AC_ARG_ENABLE(xinput, AS_HELP_STRING([--enable-xinput], [Build XCB XInput Extension (EXPERIMENTAL) (default: no)]), [BUILD_XINPUT=$enableval], [BUILD_XINPUT=no])
AM_CONDITIONAL(BUILD_XINPUT, [test "x$BUILD_XINPUT" = xyes])
AC_ARG_ENABLE(xprint, AS_HELP_STRING([--enable-xprint], [Build XCB Xprint Extension (default: yes)]), [BUILD_XPRINT=$enableval], [BUILD_XPRINT=yes])
AM_CONDITIONAL(BUILD_XPRINT, [test "x$BUILD_XPRINT" = xyes])
AC_ARG_ENABLE(xselinux, AS_HELP_STRING([--enable-xselinux], [Build XCB SELinux Extension (default: no)]), [BUILD_SELINUX=$enableval], [BUILD_SELINUX=no])
AM_CONDITIONAL(BUILD_SELINUX, [test "x$BUILD_SELINUX" = xyes])
AC_ARG_ENABLE(xtest, AS_HELP_STRING([--enable-xtest], [Build XCB XTEST Extension (default: yes)]), [BUILD_XTEST=$enableval], [BUILD_XTEST=yes])
AM_CONDITIONAL(BUILD_XTEST, [test "x$BUILD_XTEST" = xyes])
AC_ARG_ENABLE(xv, AS_HELP_STRING([--enable-xv], [Build XCB Xv Extension (default: yes)]), [BUILD_XV=$enableval], [BUILD_XV=yes])
AM_CONDITIONAL(BUILD_XV, [test "x$BUILD_XV" = xyes])
AC_ARG_ENABLE(xvmc, AS_HELP_STRING([--enable-xvmc], [Build XCB XvMC Extension (default: yes)]), [BUILD_XVMC=$enableval], [BUILD_XVMC=yes])
AM_CONDITIONAL(BUILD_XVMC, [test "x$BUILD_XVMC" = xyes])


dnl ================================================================
dnl Compiler stuff.
dnl ================================================================
AM_PROG_CC_C_O
AC_ISC_POSIX
AC_C_CONST


dnl ================================================================
dnl Libtool stuff.
dnl ================================================================
AC_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --preserve-dup-deps"


dnl ================================================================
dnl I18n stuff.
dnl ================================================================
dnl AC_PROG_INTLTOOL([0.23])
dnl GETTEXT_PACKAGE=gx
dnl AC_SUBST(GETTEXT_PACKAGE)
dnl AC_SUBST([CONFIG_STATUS_DEPENDENCIES],['$(top_srcdir)/po/LINGUAS'])
dnl AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])
dnl ALL_LINGUAS="`cat "$srcdir/po/LINGUAS" | grep -v '^#'`"
dnl gxlocaledir='${prefix}/${DATADIRNAME}/locale'
dnl AC_SUBST(gxlocaledir)
dnl AM_GLIB_GNU_GETTEXT


dnl ================================================================
dnl Documentation stuff.
dnl ================================================================
GTK_DOC_CHECK(1.4)
GNOME_DOC_INIT


dnl ================================================================
dnl Check for dependency packages.
dnl ================================================================
PKG_CHECK_MODULES(GXGEN_DEP, [
		  xcb-proto >= 1.1
                  glib-2.0 >= 2.16
                  libxml-2.0
])
AC_SUBST(GXGEN_DEP_CFLAGS)
AC_SUBST(GXGEN_DEP_LIBS)

PKG_CHECK_MODULES(GX_DEP, [
                  xcb
		  glib-2.0 >= 2.16
                  gobject-2.0
])
AC_SUBST(GX_DEP_CFLAGS)
AC_SUBST(GX_DEP_LIBS)

# Find the xcb-proto protocol descriptions
AC_MSG_CHECKING(XCBPROTO_XCBINCLUDEDIR)
XCBPROTO_XCBINCLUDEDIR=`$PKG_CONFIG --variable=xcbincludedir xcb-proto`
AC_MSG_RESULT($XCBPROTO_XCBINCLUDEDIR)
AC_SUBST(XCBPROTO_XCBINCLUDEDIR)


dnl ================================================================
dnl Misc program dependencies.
dnl ================================================================
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PROG_INSTALL


dnl ================================================================
dnl Checks for header files.
dnl ================================================================
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h signal.h)


dnl ================================================================
dnl Checks for library functions.
dnl ================================================================
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(putenv strdup)


dnl ================================================================
dnl What needs to be substituted in other files
dnl ================================================================
AC_SUBST(EXTRA_CFLAGS)
AC_SUBST(EXTRA_CPPFLAGS)


dnl ================================================================
dnl Dah Da!
dnl ================================================================
AC_OUTPUT(
Makefile
tools/Makefile
src/Makefile
tests/Makefile
dnl po/Makefile.in
gx-0.1.pc
)

dnl Configuration output

echo ""
echo "  Package: ${PACKAGE_NAME} ${PACKAGE_VERSION}"
echo ""
echo "  Configuration"
echo ""
echo "  Used CFLAGS:"
echo "    CPPFLAGS............: ${CPPFLAGS} ${EXTRA_CPPFLAGS}"
echo "    CFLAGS..............: ${CFLAGS} ${EXTRA_CFLAGS}"
echo ""
echo "  Installation:"
echo "    Prefix..............: ${prefix}"
echo ""

